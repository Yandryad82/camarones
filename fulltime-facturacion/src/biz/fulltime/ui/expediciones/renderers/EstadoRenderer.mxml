<?xml version="1.0" encoding="utf-8"?>
<s:MXAdvancedDataGridItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:s="library://ns.adobe.com/flex/spark"
								  focusEnabled="false">

	<fx:Script>
		<![CDATA[
			import biz.fulltime.dto.AgendaTareaDTO;
			
			import mx.controls.advancedDataGridClasses.AdvancedDataGridListData;
			import mx.controls.listClasses.BaseListData;
			
			private var _prioridad:String;
			
			private var _dirty:Boolean;
			
			[Bindable]
			private var _colorBackground:uint = 0xFFFFFF;
			
			[Bindable]
			private var _colorForeground:uint = 0x000000;
			
			[Bindable]
			private var _alpha:Number = .16;
			
			override public function set listData(value:BaseListData):void {
				super.listData = value;
				if (value) {
					
					var agendaTarea:AgendaTareaDTO = AdvancedDataGridListData(value).item as AgendaTareaDTO;
					if (agendaTarea) {					
						prioridad = agendaTarea.prioridad;
						if (agendaTarea.estado == "C") {
							if (agendaTarea.fechaHoraFin == null) {
								_colorBackground = 0xFFFF00;
								_colorForeground = 0x000000;
								_alpha = .24;
							} else {
								_colorBackground = 0x00FF00;
								_colorForeground = 0x000000;
								_alpha = .02;
							} 							
						} else if (agendaTarea.estado == "P") {
							var fechaInicio:Date = agendaTarea.fechaInicio;
							var fecha:Date = agendaTarea.fechaHora;
							
							if (fecha.date != fechaInicio.date || fecha.month != fechaInicio.month) {
								_colorBackground = 0xFF0000;
								_colorForeground = 0x000000;
								_alpha = .015;
							} else{
								_colorBackground = 0xDADADA;
								_colorForeground = 0x000000;
								_alpha = .24;
							}
							
						} else {
							_colorBackground = 0x000000;
							_colorForeground = 0x222222;
							_alpha = .84;						
						}
					}			
					_dirty = true;
					
					invalidateProperties();
				}
			}
			
			override protected function commitProperties():void {
				super.commitProperties();
				
				if (_dirty) {
					_dirty = false;
					
					icon.source = PrioridadExpedicionFactory.getPrioridadImage(_prioridad);
					toolTip = PrioridadExpedicionFactory.getPrioridadTooltip(_prioridad);
				}
			}


			public function get prioridad():String {
				return _prioridad;
			}

			public function set prioridad(value:String):void {
				_prioridad = value;
			}

		]]>
	</fx:Script>

	<s:Rect left="0" right="0" top="-2" bottom="-2">
		<s:fill>
			<s:LinearGradient rotation="90">
				<s:GradientEntry color="0xFFFFFF" alpha="{_alpha*4}" ratio=".00"/>
				<s:GradientEntry color="{_colorBackground}" alpha="{_alpha}" ratio=".09" />
				<s:GradientEntry color="{_colorBackground}" alpha="{_alpha}" ratio=".96"/>
				<s:GradientEntry color="{_colorBackground}" alpha="{_alpha*2}" ratio=".96"/>
			</s:LinearGradient>
		</s:fill>
	</s:Rect>	

	<s:BitmapImage id="icon" x="2" y="2" width="16" height="16" verticalCenter="0"/>

</s:MXAdvancedDataGridItemRenderer>
